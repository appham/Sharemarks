apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.appham.sharemarks"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1." + versionCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    applicationVariants.all { variant ->
        if (variant.getBuildType().isMinifyEnabled()) {
            if (variant != null && variant.mappingFile != null) {
                variant.assemble.doLast {
                    copy {
                        from variant.mappingFile
                        into "${projectDir}/../proguard"
                        rename { String fileName ->
                            "mapping-${variant.name}-${variant.versionName}.txt"
                        }
                    }
                    copy {
                        from variant.outputs[0].outputFile
                        into "${projectDir}/../releases"
                        rename { String fileName ->
                            "sharemarks-${variant.name}-${variant.versionName}.apk"
                        }
                    }
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    support_lib_version = "27.1.1"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:cardview-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.google.firebase:firebase-core:16.0.3"
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.jskierbi:cupboard-kotlin:+'
    implementation 'nl.qbusict:cupboard:2.2.0' // cupboard-kotlin does not include cupboard itself
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version" // cupboard-kotlin requires both kotlin-stdlib
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version" // and kotlin-reflect
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'org.jsoup:jsoup:1.9.2'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'jp.wasabeef:picasso-transformations:2.1.2'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'

    debugImplementation 'im.dino:dbinspector:3.4.1@aar'

    // Cool logcat monitoring service see https://github.com/donfuxx/LogKitten
    debugImplementation 'com.github.donfuxx:LogKitten:1.2.1'

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'

    androidTestImplementation "com.android.support:support-annotations:$support_lib_version"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:+') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
}


apply plugin: 'com.google.gms.google-services'